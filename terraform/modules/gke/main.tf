resource "google_container_cluster" "primary" {
  name     = var.cluster_name
  location = var.zone

  network    = var.network_id
  subnetwork = var.subnet_id

  # O node pool default e removido para que todo o pool gerenciado seja
  # descrito em codigo (google_container_node_pool abaixo).
  remove_default_node_pool = true
  initial_node_count       = 1

  ip_allocation_policy {
    cluster_secondary_range_name  = var.pods_range_name
    services_secondary_range_name = var.services_range_name
  }

  # Workload Identity: os pods assumem Service Accounts do GCP sem nenhuma
  # chave JSON montada no container. E a resposta direta ao problema
  # "credenciais passadas em arquivos de texto sem seguranca" do enunciado.
  workload_identity_config {
    workload_pool = "${var.project_id}.svc.id.goog"
  }

  release_channel {
    channel = "REGULAR"
  }

  deletion_protection = false
}

resource "google_container_node_pool" "primary_nodes" {
  name     = "${var.cluster_name}-node-pool"
  location = var.zone
  cluster  = google_container_cluster.primary.name

  initial_node_count = var.node_desired_count

  autoscaling {
    min_node_count = var.node_min_count
    max_node_count = var.node_max_count
  }

  node_config {
    machine_type = var.node_machine_type
    disk_size_gb = 30
    disk_type    = "pd-balanced"

    # Obriga os pods a passarem pelo metadata server do GKE (Workload
    # Identity) em vez de herdarem a SA do node.
    workload_metadata_config {
      mode = "GKE_METADATA"
    }

    oauth_scopes = [
      "https://www.googleapis.com/auth/cloud-platform",
    ]

    shielded_instance_config {
      enable_secure_boot          = true
      enable_integrity_monitoring = true
    }

    labels = {
      env = "fase3"
    }
  }

  management {
    auto_repair  = true
    auto_upgrade = true
  }
}
